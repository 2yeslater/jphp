allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = 'org.develnext'
    version = '0.4'
    isSnapshot = true;

    if (isSnapshot) {
        version += '-SNAPSHOT'
    }

    repositories {
        mavenCentral()
    }
}

configure([project(':jphp-core'), project(':jphp-swing')]) {

    if (!project.isSnapshot) {
        task sourcesJar(type: Jar, dependsOn: 'classes') {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        jar.dependsOn += [sourcesJar, javadocJar]

        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }

    task deploy << {
        ext {
            username = System.console().readLine("\nusername: ")
            password = System.console().readPassword("password: ").toString()
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: 'http://maven.develnext.org/repository/internal') {
                        authentication(userName: deploy.username, password: deploy.password);
                    }
                    snapshotRepository(url: 'http://maven.develnext.org/repository/snapshots') {
                        authentication(userName: deploy.username, password: deploy.password);
                    }
                }
            }
        }
        uploadArchives.execute();
    }

    deploy.dependsOn += jar
}
