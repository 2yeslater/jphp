allprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java'

    project.group = 'org.develnext'
    project.version = '0.5'
    project.ext.isSnapshot = System.getProperty("release", "0") == "0";

    if (project.ext.isSnapshot) {
        version += '-SNAPSHOT'
    }

    repositories {
        mavenCentral()
    }

    ext.distPath = "$rootProject.buildDir/dist"

    task collectJars(type: Copy, overwrite: true) {
        into "$project.ext.distPath/libs"
        from configurations.runtime
        exclude "jphp-*"
    }

    task copyRuntimeLibs(type: Copy, dependsOn: "jar", overwrite: true) {
        into "$project.ext.distPath/libs"
        from "$libsDir/" + jar.archiveName
        rename jar.archiveName, "$jar.baseName." + jar.extension
    }
    
    task buildPortableDef(dependsOn: [copyRuntimeLibs, collectJars], overwrite: true) {
        copy {
            from "sdk/"
            into "$project.ext.distPath/sdk"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.1";
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip";
}

task distZip (type: Zip) {
    archiveName "dist-" + version + ".zip"
    from project.ext.distPath
    destinationDir buildDir
}

configure(subprojects) {
    apply plugin: 'maven'
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    sourceSets {
        main.java.srcDirs = ['src/main/java']
        main.resources.srcDirs = ['src/main/resources']

        test.java.srcDirs = ["src/main/tests"]
        test.resources.srcDirs = ["src/main/tests"]
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}

// extensions
configure(subprojects.findAll { it.name.endsWith("-ext") }) {
    task extensionJar(type: Jar, dependsOn: "classes") {
        extensionJar.archiveName = project.name + ".jar"

        from {
            configurations.compile.findAll {
                !it.toString().contains("jphp-") && !it.toString().contains("asm-")
            }.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        from sourceSets.main.output.classesDir
    }
}

// for deploy
configure([
        project(':jphp-runtime'),
        project(':jphp-core'),
        project(':jphp-android'),
        project(':jphp-android-tools'),
        project(':jphp-swing-ext'),
        project(':jphp-zend-ext'),
        project(':jphp-json-ext'),
        project(':jphp-http-ext'),
        project(':jphp-scripting'),
        project(':jphp-gradle-plugin'),
]) {

    if (!project.ext.isSnapshot) {
        task sourcesJar(type: Jar, dependsOn: 'classes') {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        jar.dependsOn += [sourcesJar, javadocJar]

        artifacts {
            archives sourcesJar
            archives javadocJar
        }

        signing {
            sign configurations.archives
        }
    }

    task deploy << {
        rootProject.ext {
            if (System.getProperty("deployUsername", null)) {
                username = System.getProperty("deployUsername", null)
                println "user: $username"
            } else {
                username = rootProject.hasProperty('username') ? rootProject.username : System.console().readLine("\nusername: ")
            }

            if (System.getProperty("deployPassword", null)) {
                password = System.getProperty("deployPassword", null)
                println()
            } else {
                password = rootProject.hasProperty('password') ? rootProject.password : System.console().readPassword("password: ").toString()
            }
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                        authentication(userName: rootProject.username, password: rootProject.password);
                    }

                    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                        authentication(userName: rootProject.username, password: rootProject.password);
                    }

                    pom.project {
                        name 'JPHP Compiler'
                        description 'Alternative language engine for PHP 5.5+'
                        packaging 'jar'
                        url 'http://github.com/jphp-compiler'

                        scm {
                            connection 'scm:git:git@github.com:jphp-compiler/jphp.git'
                            developerConnection 'scm:git:git@github.com:jphp-compiler/jphp.git'
                            url 'git@github.com:jphp-compiler/jphp.git'
                        }

                        licenses {
                            license {
                                name 'The Apache License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id 'dim-s'
                                name 'Dmitriy Zayceff'
                                email 'd.zayceff@gmail.com'
                            }
                        }
                    }
                }
            }
        }
        uploadArchives.execute();
    }

    deploy.dependsOn += jar
}
